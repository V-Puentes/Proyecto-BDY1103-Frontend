CREATE OR REPLACE PROCEDURE PROC_KPI6_RECURRENCIA IS

--==================Definicion de variables==================--

C_PROC_NAME CONSTANT VARCHAR2(30) := 'PROC_KPI6_RECURRENCIA';

-- record para variables
TYPE KPI_REC IS RECORD (
    ANIO                KPI_6.ANIO%TYPE,
    TOTAL_CLIENTES_UNQ  KPI_6.TOTAL_CLIENTES_UNQ%TYPE,
    TASA_RECURRENCIA    KPI_6.TASA_RECURRENCIA%TYPE,
    VARIACION_TASA      KPI_6.VARIACION_TASA%TYPE,
    FECHA_ACTUALIZACION KPI_6.FECHA_ACTUALIZACION%TYPE
);

-- Colección para resultados
TYPE kpi6_array IS TABLE OF KPI_REC;
RESULTADOS_KPI6 kpi6_array;

--==================Inicio de los procesos==================--
BEGIN
-- Limpieza de tabla de destino
DELETE FROM KPI_6;

-- 2. Cálculo Masivo con SQL (Agregación y Analíticas)
WITH FRECUENCIA_ANUAL AS (
    SELECT
        TO_CHAR(FECHA_COM, 'YYYY') AS ANIO,
        CLIENTE_RUN_CLI,
        COUNT(COD_COM) AS COMPRAS_TOTALES
    FROM
        COMPRA
    GROUP BY
        TO_CHAR(FECHA_COM, 'YYYY'), CLIENTE_RUN_CLI
),

TASAS_ANUALES AS (
    SELECT
        FA.ANIO,
        COUNT(FA.CLIENTE_RUN_CLI) AS TOTAL_CLIENTES_UNQ,
        SUM(CASE WHEN FA.COMPRAS_TOTALES > 1 THEN 1 ELSE 0 END) AS CLIENTES_RECURRENTES,
        ROUND((SUM(CASE WHEN FA.COMPRAS_TOTALES > 1 THEN 1 ELSE 0 END) / COUNT(FA.CLIENTE_RUN_CLI)) * 100, 2) AS TASA_ACTUAL
    FROM
        FRECUENCIA_ANUAL FA
    GROUP BY
        FA.ANIO
    ORDER BY
        FA.ANIO
)
--Colección de resultados
SELECT
    TA.ANIO AS ANIO,
    TA.TOTAL_CLIENTES_UNQ AS TOTAL_CLIENTES_UNQ,
    TA.TASA_ACTUAL AS TASA_RECURRENCIA,
    CASE 
        WHEN LAG(TA.TASA_ACTUAL, 1, NULL) OVER (ORDER BY TA.ANIO) IS NOT NULL
        THEN ROUND(TA.TASA_ACTUAL - LAG(TA.TASA_ACTUAL, 1) OVER (ORDER BY TA.ANIO), 2)
        ELSE NULL
    END AS VARIACION_TASA,
    SYSDATE AS FECHA_ACTUALIZACION
BULK COLLECT INTO RESULTADOS_KPI6
FROM
    TASAS_ANUALES TA;
    
-- Inserción de resultados
IF RESULTADOS_KPI6.COUNT > 0 THEN
    FORALL i IN RESULTADOS_KPI6.FIRST .. RESULTADOS_KPI6.LAST
        INSERT INTO KPI_6 (
            ANIO, TOTAL_CLIENTES_UNQ, TASA_RECURRENCIA, VARIACION_TASA, FECHA_ACTUALIZACION
        )
        VALUES (
            RESULTADOS_KPI6(i).ANIO,
            RESULTADOS_KPI6(i).TOTAL_CLIENTES_UNQ,
            RESULTADOS_KPI6(i).TASA_RECURRENCIA,
            RESULTADOS_KPI6(i).VARIACION_TASA,
            RESULTADOS_KPI6(i).FECHA_ACTUALIZACION
        );
END IF;

EXCEPTION
WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('--- ERROR EN EL PROCEDIMIENTO KPI 6 ---');
    DBMS_OUTPUT.PUT_LINE('Procedimiento: ' || C_PROC_NAME);
    DBMS_OUTPUT.PUT_LINE('Código SQL: ' || SQLCODE);
    DBMS_OUTPUT.PUT_LINE('Mensaje SQL: ' || SQLERRM);
    RAISE;
END PROC_KPI6_RECURRENCIA