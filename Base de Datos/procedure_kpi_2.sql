CREATE OR REPLACE PROCEDURE PROC_KPI2_VENTAS IS

--==================Definicion de variables==================--

-- Variable para manejo de errores
C_PROC_NAME CONSTANT VARCHAR2(30) := 'PROC_KPI2_VENTAS';

-- record para variables
TYPE KPI2_REC IS RECORD (
    PERIODO             KPI_2.PERIODO%TYPE,
    TOTAL_NETO         KPI_2.TOTAL_NETO%TYPE,
    VARIACION_PORCENTAJE KPI_2.VARIACION_PORCENTAJE%TYPE,
    FECHA_ACTUALIZACION KPI_2.FECHA_ACTUALIZACION%TYPE
);

-- Colección para resultados
TYPE kpi2_array IS TABLE OF KPI2_REC;
RESULTADOS_KPI2 kpi2_array;

--==================Inicio de los procesos==================--
BEGIN
-- Vaciamos la tabla de destino
DELETE FROM KPI_2;

-- Cálculo de ventas y variación

WITH VENTAS_MENSUALES AS (
    SELECT
        TRUNC(FECHA_COM, 'MM') AS MES,
        SUM(TOTAL_SIN_IVA) AS TOTAL_NETO,
        LAG(SUM(TOTAL_SIN_IVA), 1, 0)
            OVER (ORDER BY TRUNC(FECHA_COM, 'MM')) AS VENTA_NETA_ANTERIOR
    FROM
        COMPRA
    WHERE
        -- Solo los últimos 12 meses
        FECHA_COM >= ADD_MONTHS(TRUNC(SYSDATE, 'MM'), -11)
    GROUP BY
        TRUNC(FECHA_COM, 'MM')
)
-- Recopilación de resultados
SELECT
    TO_CHAR(VM.MES, 'Month YYYY', 'NLS_DATE_LANGUAGE=SPANISH') AS PERIODO,
    VM.TOTAL_NETO AS TOTAL_NETO,

    CASE
        WHEN VM.VENTA_NETA_ANTERIOR > 0
        THEN ROUND(((VM.TOTAL_NETO - VM.VENTA_NETA_ANTERIOR) / VM.VENTA_NETA_ANTERIOR) * 100, 2)
        ELSE NULL
    END AS VARIACION_PORCENTAJE,
    SYSDATE AS FECHA_ACTUALIZACION
BULK COLLECT INTO RESULTADOS_KPI2
FROM
    VENTAS_MENSUALES VM
ORDER BY
    VM.MES;

--Inserción de resultados
IF RESULTADOS_KPI2.COUNT > 0 THEN
    FORALL i IN RESULTADOS_KPI2.FIRST .. RESULTADOS_KPI2.LAST
        INSERT INTO KPI_2 (
            PERIODO, TOTAL_NETO, VARIACION_PORCENTAJE, FECHA_ACTUALIZACION
        )
        VALUES (
            RESULTADOS_KPI2(i).PERIODO,
            RESULTADOS_KPI2(i).TOTAL_NETO,
            RESULTADOS_KPI2(i).VARIACION_PORCENTAJE,
            RESULTADOS_KPI2(i).FECHA_ACTUALIZACION
        );
END IF;

EXCEPTION
WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('Error en KPI_2: ' || SQLERRM);
    RAISE;
END PROC_KPI2_VENTAS;
/