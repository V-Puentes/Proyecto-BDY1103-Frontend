CREATE OR REPLACE PROCEDURE PROC_KPI3_COMPRAS IS

--==================Definicion de variables==================--

--variable para control de errores
C_PROC_NAME CONSTANT VARCHAR2(30) := 'PROC_KPI3_COMPRAS';

-- record para variables
TYPE KPI3_REC IS RECORD (
    PERIODO             KPI_3.PERIODO%TYPE,
    PROMEDIO_COMPRA     KPI_3.PROMEDIO_COMPRA%TYPE,
    MEDIANA_COMPRA      KPI_3.MEDIANA_COMPRA%TYPE,
    VALOR_MODAL         KPI_3.VALOR_MODAL%TYPE,
    CANTIDAD_COMPRAS    KPI_3.CANTIDAD_COMPRAS%TYPE,
    VARIACION_PROMEDIO  KPI_3.VARIACION_PROMEDIO%TYPE,
    FECHA_ACTUALIZACION KPI_3.FECHA_ACTUALIZACION%TYPE
);

-- Colección para resultados
TYPE kpi3_array IS TABLE OF KPI3_REC;
RESULTADOS_KPI3 kpi3_array;

--==================Inicio de los procesos==================--
BEGIN

-- Vaciamos la tabla de destino
DELETE FROM KPI_3;

-- Cálculo de Moda, Mediana y Promedio
WITH COMPRAS_MENSUALES AS (
    SELECT
        TRUNC(FECHA_COM, 'MM') AS MES,
        AVG(TOTAL_CON_IVA) AS PROMEDIO,
        COUNT(COD_COM) AS CANTIDAD_COMPRAS,
        LAG(AVG(TOTAL_CON_IVA), 1, 0)
            OVER (ORDER BY TRUNC(FECHA_COM, 'MM')) AS PROMEDIO_ANTERIOR
    FROM
        COMPRA
    WHERE
        -- Solo ultimos 12 meses
        FECHA_COM >= ADD_MONTHS(TRUNC(SYSDATE, 'MM'), -11)
    GROUP BY
        TRUNC(FECHA_COM, 'MM')
)
-- Recolección de resultados
SELECT
    TO_CHAR(CM.MES, 'Month YYYY', 'NLS_DATE_LANGUAGE=SPANISH') AS PERIODO,
    CM.PROMEDIO AS PROMEDIO_COMPRA,
    (SELECT MEDIAN(TOTAL_CON_IVA)
        FROM COMPRA
        WHERE TRUNC(FECHA_COM, 'MM') = CM.MES
    )
    AS MEDIANA_COMPRA,
    (SELECT STATS_MODE(TOTAL_CON_IVA)
        FROM COMPRA 
        WHERE TRUNC(FECHA_COM, 'MM') = CM.MES
    )
    AS VALOR_MODAL,
    CM.CANTIDAD_COMPRAS AS CANTIDAD_COMPRAS,
    
    -- Cálculo de la variación
    CASE
        WHEN CM.PROMEDIO_ANTERIOR > 0
        THEN ROUND(((CM.PROMEDIO - CM.PROMEDIO_ANTERIOR) / CM.PROMEDIO_ANTERIOR) * 100, 2)
        ELSE NULL
    END AS VARIACION_PROMEDIO,
    SYSDATE AS FECHA_ACTUALIZACION
BULK COLLECT INTO RESULTADOS_KPI3
FROM
    COMPRAS_MENSUALES CM
ORDER BY
    CM.MES;

-- Inserción de resultados
IF RESULTADOS_KPI3.COUNT > 0 THEN
    FORALL i IN RESULTADOS_KPI3.FIRST .. RESULTADOS_KPI3.LAST
        INSERT INTO KPI_3 (
            PERIODO, PROMEDIO_COMPRA, MEDIANA_COMPRA, VALOR_MODAL,
            CANTIDAD_COMPRAS, VARIACION_PROMEDIO, FECHA_ACTUALIZACION
        )
        VALUES (
            RESULTADOS_KPI3(i).PERIODO,
            RESULTADOS_KPI3(i).PROMEDIO_COMPRA,
            RESULTADOS_KPI3(i).MEDIANA_COMPRA,
            RESULTADOS_KPI3(i).VALOR_MODAL,
            RESULTADOS_KPI3(i).CANTIDAD_COMPRAS,
            RESULTADOS_KPI3(i).VARIACION_PROMEDIO,
            RESULTADOS_KPI3(i).FECHA_ACTUALIZACION
        );
END IF;

EXCEPTION
WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('--- ERROR EN EL PROCEDIMIENTO KPI 3 ---');
    DBMS_OUTPUTT.PUT_LINE('Procedimiento: ' || C_PROC_NAME);
    DBMS_OUTPUT.PUT_LINE('Código SQL: ' || SQLCODE);
    DBMS_OUTPUT.PUT_LINE('Mensaje SQL: ' || SQLERRM);
    RAISE;
END PROC_KPI3_COMPRAS;
/