CREATE OR REPLACE PROCEDURE PROC_REGISTROS_POR_MES IS

--==================Definicion de variables==================--

-- variable para manejo de errores
C_PROC_NAME CONSTANT VARCHAR2(30) := 'PROC_REGISTROS_POR_MES';

-- RECORD
TYPE T_KPI1_REC IS RECORD (
    PERIODO_REC         KPI_1.PERIODO%TYPE,
    TOT_REGISTROS_REC   KPI_1.TOTAL_REGISTROS%TYPE
);
-- Colección para resultados
TYPE T_KPI1_ARRAY IS TABLE OF T_KPI1_REC;

-- Colección de los resultados.
ARRAY_RESULTADOS T_KPI1_ARRAY;
V_FECHA_INICIO_PERIODO CONSTANT DATE := ADD_MONTHS(TRUNC(SYSDATE, 'MM'), -11);

--==================Inicio de los procesos==================--

BEGIN
--vaciar tabla destino
DELETE FROM KPI_1;

-- Conteo por mes y filtrado de los últimos 12 meses
SELECT
    TO_CHAR(TRUNC(c.FECHA_REG_CLI, 'MM'), 'Month YYYY', 'NLS_DATE_LANGUAGE=SPANISH') AS PERIODO_REC,
    COUNT(c.RUN_CLI) AS TOT_REGISTROS_REC

BULK COLLECT INTO ARRAY_RESULTADOS

FROM
    CLIENTE c
WHERE
    c.FECHA_REG_CLI IS NOT NULL
    AND c.FECHA_REG_CLI >= V_FECHA_INICIO_PERIODO
    AND c.FECHA_REG_CLI <= TRUNC(SYSDATE, 'MM') + INTERVAL '1' MONTH - INTERVAL '1' SECOND 

GROUP BY
    TRUNC(c.FECHA_REG_CLI, 'MM')
ORDER BY 
    TRUNC(c.FECHA_REG_CLI, 'MM');

-- Inserción de los datos recolectados
IF ARRAY_RESULTADOS.COUNT > 0 THEN
    FORALL i IN ARRAY_RESULTADOS.FIRST .. ARRAY_RESULTADOS.LAST
        INSERT INTO KPI_1 (PERIODO, TOTAL_REGISTROS)
        VALUES (TRIM(ARRAY_RESULTADOS(i).PERIODO_REC), ARRAY_RESULTADOS(i).TOT_REGISTROS_REC);
END IF;

--==================Manejo de excepciones==================--
EXCEPTION
    WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE('--- ERROR EN EL PROCEDIMIENTO KPI 1 ---');
        DBMS_OUTPUT.PUT_LINE('Procedimiento: ' || C_PROC_NAME);
        DBMS_OUTPUT.PUT_LINE('Código SQL: ' || SQLCODE);
        DBMS_OUTPUT.PUT_LINE('Mensaje SQL: ' || SQLERRM);
        RAISE;

END PROC_REGISTROS_POR_MES;
/