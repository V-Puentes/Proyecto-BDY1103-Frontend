CREATE OR REPLACE PROCEDURE PROC_KPI5_TASA_ENTREGAS IS

--==================Definicion de variables==================--

C_PROC_NAME CONSTANT VARCHAR2(30) := 'PROC_KPI5_TASA_ENTREGAS';

-- RECORD para variables
TYPE KPI5_REC IS RECORD (
    PERIODO             KPI_5.PERIODO%TYPE,
    TASA_EXITOSAS       KPI_5.TASA_EXITOSAS%TYPE,
    VARIACION_TASA      KPI_5.VARIACION_TASA%TYPE,
    FECHA_ACTUALIZACION KPI_5.FECHA_ACTUALIZACION%TYPE
);

-- Colección para resultados
TYPE kpi5_array IS TABLE OF KPI5_REC;
RESULTADOS_KPI5 kpi5_array;

--==================Inicio de los procesos==================--
BEGIN
-- Limpieza de tabla de destino
DELETE FROM KPI_5;

-- Cálculo de la tasa de entregas exitosas
WITH ENTREGAS_MENSUALES AS (
    SELECT
        TRUNC(FECHA_ENT_FIN, 'MM') AS MES,
        COUNT(ID_ENTREGA) AS TOTAL_ENTREGAS_PROCESADAS,
        SUM(CASE WHEN UPPER(ESTADO_ENT) = 'ENTREGADO' THEN 1 ELSE 0 END) AS ENTREGAS_EXITOSAS
    FROM
        ENTREGA
    WHERE
        FECHA_ENT_FIN IS NOT NULL
        AND UPPER(ESTADO_ENT) IN ('ENTREGADO', 'ENTREGA FALLIDA')
        AND FECHA_ENT_FIN >= ADD_MONTHS(TRUNC(SYSDATE, 'MM'), -11)
    GROUP BY
        TRUNC(FECHA_ENT_FIN, 'MM')
),

TASAS_CALCULADAS AS (
    SELECT
        MES,
        CASE 
            WHEN EM.TOTAL_ENTREGAS_PROCESADAS > 0
            THEN ROUND((EM.ENTREGAS_EXITOSAS / EM.TOTAL_ENTREGAS_PROCESADAS) * 100, 2)
            ELSE 0 
        END AS TASA_ACTUAL,
        LAG(
            CASE WHEN EM.TOTAL_ENTREGAS_PROCESADAS > 0 THEN ROUND((EM.ENTREGAS_EXITOSAS / EM.TOTAL_ENTREGAS_PROCESADAS) * 100, 2) ELSE 0 END,
            1,
            NULL
        ) OVER (ORDER BY MES) AS TASA_ANTERIOR
    FROM
        ENTREGAS_MENSUALES EM
)

-- Recolección de resultados
SELECT
    TO_CHAR(TC.MES, 'Month YYYY', 'NLS_DATE_LANGUAGE=SPANISH') AS PERIODO,
    TC.TASA_ACTUAL AS TASA_EXITOSAS,
    CASE
        WHEN TC.TASA_ANTERIOR IS NOT NULL
        THEN ROUND(TC.TASA_ACTUAL - TC.TASA_ANTERIOR, 2)
        ELSE NULL
    END AS VARIACION_TASA,
    SYSDATE AS FECHA_ACTUALIZACION
BULK COLLECT INTO RESULTADOS_KPI5
FROM
    TASAS_CALCULADAS TC
ORDER BY
    TC.MES;

-- Inserción de resultados
IF RESULTADOS_KPI5.COUNT > 0 THEN
    FORALL i IN RESULTADOS_KPI5.FIRST .. RESULTADOS_KPI5.LAST
        INSERT INTO KPI_5 (
            PERIODO, TASA_EXITOSAS, VARIACION_TASA, FECHA_ACTUALIZACION
        )
        VALUES (
            RESULTADOS_KPI5(i).PERIODO,
            RESULTADOS_KPI5(i).TASA_EXITOSAS,
            RESULTADOS_KPI5(i).VARIACION_TASA,
            RESULTADOS_KPI5(i).FECHA_ACTUALIZACION
        );
END IF;

EXCEPTION
WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('--- ERROR EN EL PROCEDIMIENTO KPI 5 ---');
    DBMS_OUTPUT.PUT_LINE('Procedimiento: ' || C_PROC_NAME);
    DBMS_OUTPUT.PUT_LINE('Código SQL: ' || SQLCODE);
    DBMS_OUTPUT.PUT_LINE('Mensaje SQL: ' || SQLERRM);
    RAISE;
END PROC_KPI5_TASA_ENTREGAS;
/