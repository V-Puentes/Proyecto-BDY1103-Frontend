CREATE OR REPLACE PROCEDURE PROC_KPI4_STOCK_VARIACION IS

--==================Definicion de variables==================--

--variable para control de errores
C_PROC_NAME CONSTANT VARCHAR2(30) := 'PROC_KPI4_STOCK_VARIACION';

-- Umbral de alerta de stock (-50% = CRÍTICO, -20% = BAJO)
C_CRITICAL_DECREASE CONSTANT NUMBER := -50;
C_LOW_DECREASE CONSTANT NUMBER := -20;

-- record con las variables
TYPE KPI4_REC IS RECORD (
    FRANQUICIA              KPI_4.FRANQUICIA%TYPE,
    STOCK_ACTUAL            PI_4.STOCK_AGREGADO_ACTUAL%TYPE,
    PRODUCTOS_BAJO_STOCK    PI_4.PRODUCTOS_BAJO_STOCK%TYPE,
    TOTAL_PRODUCTOS          KPI_4.TOTAL_PRODUCTOS%TYPE,
    STOCK_ANTERIOR_REF   KPI_4.STOCK_AGREGADO_ACTUAL%TYPE,
    VARIACION_PORCENTAJE    PI_4.VARIACION_PORCENTAJE%TYPE,
    ESTADO_STOCK            PI_4.ESTADO_STOCK%TYPE,
    FECHA_ACTUALIZACION     KPI_4.FECHA_ACTUALIZACION%TYPE
);

-- Colección para resultados
TYPE kpi4_array IS TABLE OF KPI4_REC;
RESULTADOS_KPI4 kpi4_array;

--==================Inicio de los procesos==================--
BEGIN

-- Cálculo de stock por franquicia
WITH STOCK_ACTUAL AS (
    SELECT
        p.FRANQUICIA_PRO AS FRANQUICIA,
        SUM(p.STOCK) AS STOCK_ACTUAL,
        SUM(CASE WHEN p.STOCK < 5 THEN 1 ELSE 0 END) AS BAJO_STOCK_COUNT,
        COUNT(p.ID_PRO) AS TOTAL_PRODS
    FROM
        PRODUCTO p
    GROUP BY
        p.FRANQUICIA_PRO
)
-- Consulta
SELECT
    SA.FRANQUICIA AS FRANQUICIA,
    SA.STOCK_ACTUAL AS STOCK_ACTUAL,
    SA.BAJO_STOCK_COUNT AS PRODUCTOS_BAJO_STOCK,
    SA.TOTAL_PRODS AS TOTAL_PRODUCTOS,
    NVL(k.STOCK_AGREGADO_ACTUAL, 0) AS STOCK_ANTERIOR_REF,
    NULL AS VARIACION_PORCENTAJE,
    NULL AS ESTADO_STOCK,
    SYSDATE AS FECHA_ACTUALIZACION

--recopilacióp resultados
BULK COLLECT INTO RESULTADOS_KPI4
FROM
    STOCK_ACTUAL SA
LEFT JOIN
    KPI_4 k ON SA.FRANQUICIA = k.FRANQUICIA;

-- loop bloque anonimo 
FOR i IN 1..RESULTADOS_KPI4.COUNT LOOP

    DECLARE
        V_ACTUAL NUMBER := RESULTADOS_KPI4(i).STOCK_ACTUAL;
        V_ANTERIOR NUMBER := RESULTADOS_KPI4(i).STOCK_ANTERIOR_REF;
        V_VARIACION_CALCULADA NUMBER(10,2) := NULL;
    BEGIN
        -- Cálculo de variación
        IF V_ANTERIOR > 0 THEN
           V_VARIACION_CALCULADA := ROUND(((V_ACTUAL - V_ANTERIOR) / V_ANTERIOR) * 100, 2);
        END IF;
        RESULTADOS_KPI4(i).VARIACION_PORCENTAJE := V_VARIACION_CALCULADA;

-- Clasificación del ESTADO_STOCK
IF V_ACTUAL = 0 THEN
RESULTADOS_KPI4(i).ESTADO_STOCK := 'AGOTADO';
ELSIF V_VARIACION_CALCULADA IS NOT NULL AND V_VARIACION_CALCULADA <= C_CRITICAL_DECREASE THEN
RESULTADOS_KPI4(i).ESTADO_STOCK := 'CRÍTICO';
ELSIF V_VARIACION_CALCULADA IS NOT NULL AND V_VARIACION_CALCULADA <= C_LOW_DECREASE THEN
RESULTADOS_KPI4(i).ESTADO_STOCK := 'BAJO';
ELSIF RESULTADOS_KPI4(i).PRODUCTOS_BAJO_STOCK > 0 THEN
RESULTADOS_KPI4(i).ESTADO_STOCK := 'ALERTA';
ELSE
RESULTADOS_KPI4(i).ESTADO_STOCK := 'NORMAL';
END IF;

    END;
END LOOP;


-- Inserción de datos (MERGE FORALL)
IF RESULTADOS_KPI4.COUNT > 0 THEN
FORALL i IN RESULTADOS_KPI4.FIRST .. RESULTADOS_KPI4.LAST
MERGE INTO KPI_4 k
USING (
    SELECT 
        RESULTADOS_KPI4(i).FRANQUICIA AS FRANQUICIA,
        RESULTADOS_KPI4(i).STOCK_ACTUAL AS STOCK_AGREGADO_ACTUAL,
        RESULTADOS_KPI4(i).PRODUCTOS_BAJO_STOCK AS PRODUCTOS_BAJO_STOCK,
        RESULTADOS_KPI4(i).TOTAL_PRODUCTOS AS TOTAL_PRODUCTOS,
        RESULTADOS_KPI4(i).VARIACION_PORCENTAJE AS VARIACION_PORCENTAJE,
        RESULTADOS_KPI4(i).ESTADO_STOCK AS ESTADO_STOCK,
        RESULTADOS_KPI4(i).FECHA_ACTUALIZACION AS FECHA_ACTUALIZACION
    FROM DUAL
) src ON (k.FRANQUICIA = src.FRANQUICIA)
WHEN MATCHED THEN
    UPDATE SET
        k.STOCK_AGREGADO_ACTUAL = src.STOCK_AGREGADO_ACTUAL,
        k.VARIACION_PORCENTAJE = src.VARIACION_PORCENTAJE,
        k.ESTADO_STOCK = src.ESTADO_STOCK,
        k.PRODUCTOS_BAJO_STOCK = src.PRODUCTOS_BAJO_STOCK,
        k.TOTAL_PRODUCTOS = src.TOTAL_PRODUCTOS,
        k.FECHA_ACTUALIZACION = src.FECHA_ACTUALIZACION
WHEN NOT MATCHED THEN
    INSERT (FRANQUICIA, STOCK_AGREGADO_ACTUAL, VARIACION_PORCENTAJE, ESTADO_STOCK, PRODUCTOS_BAJO_STOCK, TOTAL_PRODUCTOS, FECHA_ACTUALIZACION)
    VALUES (src.FRANQUICIA, src.STOCK_AGREGADO_ACTUAL, src.VARIACION_PORCENTAJE, src.ESTADO_STOCK, src.PRODUCTOS_BAJO_STOCK, src.TOTAL_PRODUCTOS, src.FECHA_ACTUALIZACION);
END IF;


EXCEPTION
WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('--- ERROR EN EL PROCEDIMIENTO KPI 4 ---');
    DBMS_OUTPUT.PUT_LINE('Procedimiento: ' || C_PROC_NAME);
    DBMS_OUTPUT.PUT_LINE('Código SQL: ' || SQLCODE);
    DBMS_OUTPUT.PUT_LINE('Mensaje SQL: ' || SQLERRM);
    RAISE;

END PROC_KPI4_STOCK_VARIACION;
/